/******************************************************************************\
FILE:           task.zm
AUTHOR:         Theo Veenker <theo.veenker@beexy.nl>
ADAPTED BY:     -

DESCRIPTION:

Implementation of task: Self-Paced Reading with Stationary Window
See readme.txt for more information.


Global structure of this experiment phase:

  - instructions

  - practice part
      trial
        stimulus presentation
        question (conditional)

  - pause

  - test part
      trial
        stimulus presentation
        question (conditional)


HISTORY:
2012-03-19 TV   Created.

\******************************************************************************/

//requires 1.10;

import std_choose_1of2_page;

import defs;
import stimuli;
import shuffle;
import output;
import test_page;


Block task
{
    on_event:entry()
    {
        control.button_box.open();

        // If necessary set parallel port line D0 to power button box device.
//        control.button_box.output(0x02);
    }


    Part instructions
    {
        on_event:entry()
        {
//            pause_page.set_text_size(1000, 600);
            pause_page.set_text(INSTRUCTION_TEXT,
                true);

            pause_page.action(this);
            control.button_box.enable_all_buttons(CONTINUE);
        }
    }


    Block practice
    {
        TestItem        item;   // current item


        on_event:entry()
        {
            num_cycles = prac_items.size;

            // Prepare test page: progress bar and prompt enabled.
            test_page.setup(num_cycles, true);
        }


        on_event:exit()
        {
            test_page.cleanup();
        }


        Trial trial
        {
            on_event:entry()
            {
                item = prac_items[practice.cycle];
            }


            SubTrial presentation
            {
                on_event:entry()
                {
                    test_page.action(this, item, reference_time, practice.cycle);
                }
            }

            SubTrial question
            {
                on_event:entry()
                {
                    choose_1of2_page.set_text(" ? ");
                    choose_1of2_page.set_label_texts(" Yes ", " No ");
                    choose_1of2_page.set_keys(KEY_Shift_L, KEY_Shift_R);
                    choose_1of2_page.action(this, item.qanswer ? 1 : 2);
                }
            }
        }
    }


    Part
    {
        on_event:entry()
        {
            blank_page.action_auto(this, 500ms);
        }
    }


    Part pause
    {
        on_event:entry()
        {
            pause_page.set_text(
              END_OF_PRACTICE_TEXT,
                true);

            pause_page.action(this);
            control.button_box.enable_all_buttons(CONTINUE);
        }
    }


    Block test
    {
        TestItem        item;   // current item
        TestOutput      output; // current output table row


        on_event:entry()
        {
            // Randomize test items table.
            if (shuffle_test_items() != OK) terminate;

            num_cycles = test_items.size;
//            num_cycles = 3;

            // Prepare test page: progress bar enabled, prompt disabled.
            test_page.setup(num_cycles, false);
        }


        on_event:exit()
        {
            test_page.cleanup();

            // Save test output table as CSV file.
            test_output_save();
        }


        Trial trial
        {
            on_event:entry()
            {
                item = test_items[test.cycle];

                output.trialnum = test.cycle + 1;
                output.id = item.id;
                output.rt = -9999;
                output.type = item.type;
                output.qanswer = item.qanswer;
                output.qcorrect = -1;
            }


            on_event:exit()
            {
                // Add new row to test output table.
                test_output_append(output);
            }


            SubTrial presentation
            {
                on_event:entry()
                {
                    test_page.action(this, item, reference_time, test.cycle);
                }


                on_event:exit()
                {
                    output.rt = test_page.response.rt[1];
                }
            }

            SubTrial question
            {
                on_event:entry()
                {
                    choose_1of2_page.set_text("?");
                    choose_1of2_page.set_label_texts(" Yes ", " No ");
                    choose_1of2_page.set_keys(KEY_Shift_L, KEY_Shift_R);
                    choose_1of2_page.action(this, item.qanswer ? 1 : 2);
                }

                on_event:exit()
                {
                    output.qcorrect = choose_1of2_page.response.correct;
                }
            }
        }
    }


    Part
    {
        on_event:entry()
        {
            blank_page.action_auto(this, 500ms);
        }
    }
}


void action(Part caller)
{
    // Activate the task. When the task is done, the registered caller
    // (c.q. the main experiment object) will automatically be signalled.
    task.enter(caller);
}
